# Yosys synthesis script for ${TOP_MODULE}

#########################
# Parse input files
#########################
# Read verilog files
read_verilog ${READ_VERILOG_OPTIONS} ${VERILOG_FILES}
# Read technology library
read_verilog -lib -specify ${YOSYS_CELL_SIM_VERILOG}

#########################
# Prepare for synthesis
#########################
# Identify top module from hierarchy
hierarchy -check -top ${TOP_MODULE}
# Convert high-level behavioral parts ("processes") to DFFs and muxes
proc
# Flatten all the gates/primitives
flatten
# Identify tri-state buffers from 'z' signal in AST
# with follow-up optimizations to clean up AST
tribuf -logic
opt_expr
opt_clean
# Demote inout ports to input or output port
# with follow-up optimizations to clean up AST
deminout
opt -nodffe -nosdff
opt_expr
opt_clean
# Check if the design is valid
check
opt -nodffe -nosdff
# Reduce the word size of operations if possible. ignore dont care
wreduce -keepdc
# Applies a collection of peephole optimizers to the current design
peepopt
# Transforms $pmux cells to trees of $mux cells
pmuxtree
opt_clean

#########################
# Map flip-flops
#########################
# Convert FFs to types supported by the target
dfflegalize -cell $_DFF_P_ 0
# Convert asynchronous FFs to synchronous FFs
techmap -map +/adff2dff.v

########################
# Map multipliers
# Inspired from synth_xilinx.cc
#########################
# Avoid merging any registers into DSP, reserve memory port registers first
memory_dff
wreduce t:$mul
techmap -map +/mul2dsp.v -map ${YOSYS_DSP_MAP_VERILOG} ${YOSYS_DSP_MAP_PARAMETERS}
select a:mul2dsp
setattr -unset mul2dsp
opt_expr -fine
wreduce
select -clear
chtype -set $mul t:$__soft_mul# Extract arithmetic functions

# Translates arithmetic operations like $add, $mul, $lt to $alu and $macc cells
alumacc
share
opt -nodffe -nosdff

#########################
# Run coarse synthesis
#########################
# Performs finite-state-machine (FSM) extraction and recoding
fsm
# Run a quick follow-up optimization to sweep out unused nets/signals
opt -fast -nodffe -nosdff

#########################
# Map logics to BRAMs
#########################
# Converts memories to multiport memory blocks
memory -nomap
opt_clean
# Converts the multi-port $mem memory cells into block ram instances
memory_bram -rules ${YOSYS_BRAM_MAP_RULES}
techmap -map ${YOSYS_BRAM_MAP_VERILOG}
opt -fast -mux_undef -undriven -fine -nodffe -nosdff
# Translates multiport memories to basic cells
memory_map
opt -undriven -fine -nodffe -nosdff

#########################
# Convert design to (logical) gate-level netlists
#########################
techmap
opt -fast -nodffe -nosdff

#########################
# Map LUTs
#########################
abc -lut ${LUT_SIZE}

#########################
# Check and show statisitics
#########################
hierarchy -check
stat

#########################
# Output netlists
#########################
opt_clean -purge
write_blif ${OUTPUT_BLIF}
